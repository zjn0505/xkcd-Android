apply plugin: 'AndResGuard'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'

andResGuard {
    mappingFile = file("./resource_mapping.txt")
    useSign = true
    keepRoot = false

    whiteList = [
            // for icon
            "R.mipmap.ic_launcher",
            "R.mipmap.ic_launcher_round",
            // for fabric
            "R.string.com.crashlytics.*",
            // for google-services
            "R.string.default_web_client_id",
            "R.string.firebase_database_url",
            "R.string.gcm_defaultSenderId",
            "R.string.google_api_key",
            "R.string.google_app_id",
            "R.string.google_crash_reporting_api_key",
            "R.string.google_storage_bucket",
            "R.string.project_id",

            "R.string.arrow_1",
            "R.string.arrow_10",
            "R.string.arrow_30"
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
    ]
}

android {
    packagingOptions {
        exclude "/META-INF/*.kotlin_module"
        exclude "**/kotlin/**"
        exclude "/META-INF/*.version"
        exclude "/META-INF/CHANGES"
        exclude "/META-INF/*.md"
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "xyz.jienan.xkcd"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 34
        versionName "2.4.4"
        vectorDrawables.useSupportLibrary = true
        resConfigs "zh-rCN"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    signingConfigs {
        releaseConfig {
            File key = file("../key.jks")
            if (key.exists()) {
                storeFile key
                Properties properties = new Properties()
                File localProperty = file('../local.properties')
                if (localProperty.exists())
                    properties.load(localProperty.newDataInputStream())
                storePassword project.hasProperty("KEYSTORE_PASS") ? KEYSTORE_PASS : (System.getenv("KEYSTORE_PASS") ? System.getenv("KEYSTORE_PASS") : properties.getProperty("KEYSTORE_PASS"))
                keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : (System.getenv("ALIAS_NAME") ? System.getenv("ALIAS_NAME") : properties.getProperty("ALIAS_NAME"))
                keyPassword project.hasProperty("ALIAS_PASS") ? ALIAS_PASS : (System.getenv("ALIAS_PASS") ? System.getenv("ALIAS_PASS") : properties.getProperty("ALIAS_PASS"))
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project-test.pro'
            applicationIdSuffix ".debug"
            multiDexEnabled false
        }

        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
            multiDexEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.3'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha03'
    implementation 'androidx.media:media:1.1.0-alpha03'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'

    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha03'
    implementation 'com.google.android.material:material:1.1.0-alpha05'

    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'
    implementation 'com.squareup:seismic:1.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation ('com.github.bumptech.glide:glide:3.8.0') {
        force = true
    }
    implementation ('com.github.bumptech.glide:okhttp3-integration:1.5.0') {
        force = true
    }
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.google.firebase:firebase-messaging:17.5.0'
    implementation 'com.dinuscxj:circleprogressbar:1.1.1'
    implementation ("com.github.piasy:BigImageViewer:1.5.6") {
        exclude group: 'com.github.piasy.BigImageViewer', module: 'FrescoImageLoader'
        exclude group: 'com.github.piasy.BigImageViewer', module: 'FrescoImageViewFactory'
        exclude group: 'com.github.piasy.BigImageViewer', module: 'GlideImageLoader'
        exclude group: 'com.github.piasy.BigImageViewer', module: 'GlideImageViewFactory'
        exclude group: 'com.github.piasy.BigImageViewer', module: 'ProgressPieIndicator'
    }
    implementation "com.crashlytics.sdk.android:crashlytics:2.9.9"
    debugImplementation "io.objectbox:objectbox-android-objectbrowser:$objectboxVersion"
    releaseImplementation "io.objectbox:objectbox-android:$objectboxVersion"
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton.rxbinding3:rxbinding:3.0.0-alpha2'
    implementation 'me.dkzwm.widget.srl:core:1.6.5.androidx'
    implementation 'me.dkzwm.widget.srl:ext-classics:1.6.5.androidx'

    implementation 'androidx.slice:slice-core:1.0.0'
    implementation 'androidx.slice:slice-builders:1.0.0'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation "androidx.arch.core:core-testing:2.0.1"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.1.1"

    implementation 'androidx.multidex:multidex:2.0.1'

}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
    }
}

apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.objectbox'
